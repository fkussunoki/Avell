/****************************************************************************
**
** esof0520.i2 - Substitui o proprama OG0520G.P
**
**************************************************************************/

assign l-soma = no.

if  it-doc-fisc.cd-trib-icm = 3 or  it-doc-fisc.cd-trib-icm = 2 then do:
    assign l-soma  = can-find(first b-it-doc-fisc where
                                    b-it-doc-fisc.cod-estabel  = it-doc-fisc.cod-estabel
                              and   b-it-doc-fisc.serie        = it-doc-fisc.serie
                              and   b-it-doc-fisc.nr-doc-fis   = it-doc-fisc.nr-doc-fis
                              and   b-it-doc-fisc.cod-emitente = it-doc-fisc.cod-emitente
                              and   b-it-doc-fisc.nat-operacao = it-doc-fisc.nat-operacao
                              and   b-it-doc-fisc.cd-trib-icm  = 1
                              and   b-it-doc-fisc.it-codigo    = it-doc-fisc.it-codigo)
                     and not l-sequencia.
end. 

if  not l-soma 
or  r-tt-tab-ocor = ? then do:

    {ofp/esof0520g.i it-doc-fisc.nat-operacao}   /* grava c-cfop */

    assign i-aux = int(it-doc-fisc.aliquota-icm /*it-doc-fisc.dec-1*/ * 100).

    find tt-tab-ocor use-index codigo 
        where  tt-tab-ocor.cod-tab    = 249
        and    tt-tab-ocor.c-campo1 = c-usuario
        and    tt-tab-ocor.c-campo2 = c-cfop
        and    tt-tab-ocor.i-campo1 = i-aux
        and    tt-tab-ocor.l-campo1 = yes /* resumo por cfop */
        and    tt-tab-ocor.l-campo2 = yes /* icms */
        and    tt-tab-ocor.l-campo3 = {1} no-error.  
     
    if  not avail tt-tab-ocor then do:
        create tt-tab-ocor.
        assign tt-tab-ocor.cod-tab            = 249
               tt-tab-ocor.cod-ocor           = i-aux-1
               tt-tab-ocor.c-campo1           = c-usuario
               tt-tab-ocor.c-campo2           = c-cfop
               tt-tab-ocor.i-campo1           = it-doc-fisc.aliquota-icm /*it-doc-fisc.dec-1*/ * 100
               tt-tab-ocor.l-campo1           = yes /* resumo por cfop */
               tt-tab-ocor.l-campo2           = yes /* icms */
               tt-tab-ocor.l-campo3           = {1}
               substr(tt-tab-ocor.char-1,1,6) = c-cfop
               tt-tab-ocor.i-formato          = i-formato-cfop
               tt-tab-ocor.cod-estabel        = doc-fiscal.cod-estabel.
    end.
    
    assign r-tt-tab-ocor = rowid(tt-tab-ocor).
end.

find tt-tab-ocor 
    where rowid(tt-tab-ocor) = r-tt-tab-ocor no-error.

&if '{&bf_dis_versao_ems}' = '2.04' or '{&bf_dis_versao_ems}' = '2.05' &then
    if  l-considera-icms-st then
        assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + it-doc-fisc.vl-tot-item.
    else
        assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + (it-doc-fisc.vl-tot-item - it-doc-fisc.dec-2).
&elseif '{&bf_dis_versao_ems}' >= '2.06' &then
    if  l-considera-icms-st then
        assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + it-doc-fisc.vl-tot-item.
    else
        assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + (it-doc-fisc.vl-tot-item - it-doc-fisc.val-icms-subst-entr).
&else
    assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + it-doc-fisc.vl-tot-item.
&endif

assign tt-tab-ocor.de-campo2 = tt-tab-ocor.de-campo2 + it-doc-fisc.vl-bicms-it
       tt-tab-ocor.de-campo3 = tt-tab-ocor.de-campo3 + it-doc-fisc.vl-icms-it
       tt-tab-ocor.de-campo4 = tt-tab-ocor.de-campo4 + (IF  doc-fiscal.tipo-nat = 3 
                                                        AND AVAIL natur-oper 
                                                        AND natur-oper.cd-trib-icm = 2
                                                            THEN it-doc-fisc.vl-tot-item 
                                                            ELSE IF  doc-fiscal.tipo-nat <> 3 
                                                                     THEN it-doc-fisc.vl-icmsnt-it
                                                                     ELSE 0 )
       tt-tab-ocor.de-campo5 = tt-tab-ocor.de-campo5 + (IF  doc-fiscal.tipo-nat = 3 
                                                        AND AVAIL natur-oper 
                                                        AND natur-oper.cd-trib-icm = 3
                                                            THEN it-doc-fisc.vl-tot-item 
                                                            ELSE IF  doc-fiscal.tipo-nat <> 3 
                                                                     THEN it-doc-fisc.vl-icmsou-it
                                                                     ELSE 0 )
       tt-tab-ocor.c-campo3  = string(decimal(tt-tab-ocor.c-campo3) 
                             + it-doc-fisc.vl-bsubs-it)
       tt-tab-ocor.c-campo4  = string(decimal(tt-tab-ocor.c-campo4) 
                             + it-doc-fisc.vl-icmsub-it)
       i-aliquota            = tt-tab-ocor.i-campo1.  

if  l-primeiro then
    assign l-primeiro = no.
if  last(it-doc-fisc.it-codigo) then do:
    assign l-primeiro = no.

    if l-imp-dif-aliq then
        assign tt-tab-ocor.c-campo5 = string(decimal(tt-tab-ocor.c-campo5) 
                                    + doc-fiscal.vl-icms-com).

    if  ((c-estado = "PE" or c-estado = "MG")
    and doc-fiscal.cod-des-merc = 2
    and de-vl-ipiou-it > 0) then 
        assign de-obs     =   de-vl-tot-item 
                              -  (de-vl-ipiou-it 
                              +   de-vl-ipint-it 
                              +   de-vl-bipi-it
                              +   de-vl-ipi-it)
               de-obs     =   if de-obs < 0 then 0 else de-obs
               tt-tab-ocor.descricao = string(decimal(tt-tab-ocor.descricao) + de-obs).                 
end.

{ofp/esof0520g.i it-doc-fisc.nat-operacao}   /* grava c-cfop */
 
find tt-tab-ocor use-index codigo
    where  tt-tab-ocor.cod-tab    = 249
    and    tt-tab-ocor.c-campo1 = c-usuario
    and    tt-tab-ocor.c-campo2 = c-cfop /* ao inv‚s de it-doc-fisc.nat-operacao */
    and    tt-tab-ocor.i-campo1 = i-aliquota
    and    tt-tab-ocor.l-campo1 = yes /* resumo por cfop */
    and    tt-tab-ocor.l-campo2 = NO /* ipi */
    and    tt-tab-ocor.l-campo3 = {1} no-error.  

if  not avail tt-tab-ocor then do:
    /*find last tt-tab-ocor 
        where tt-tab-ocor.cod-tab = 249 no-lock no-error.
    assign i-aux-1 = (if avail tt-tab-ocor then tt-tab-ocor.cod-ocor else 0) + 1.*/
    create tt-tab-ocor.
    assign tt-tab-ocor.cod-tab    = 249
           tt-tab-ocor.cod-ocor   = i-aux-1
           tt-tab-ocor.c-campo1 = c-usuario
           tt-tab-ocor.c-campo2 = c-cfop
           tt-tab-ocor.i-campo1 = i-aliquota
           tt-tab-ocor.l-campo1 = yes /* resumo por cfop */
           tt-tab-ocor.l-campo2 = no /* ipi */
           tt-tab-ocor.l-campo3 = {1}
           substr(tt-tab-ocor.char-1,1,6) = c-cfop
           tt-tab-ocor.i-formato = i-formato-cfop
           tt-tab-ocor.cod-estabel = doc-fiscal.cod-estabel.
end.

assign tt-tab-ocor.de-campo1 = tt-tab-ocor.de-campo1 + it-doc-fisc.vl-bipi-it
       tt-tab-ocor.de-campo2 = tt-tab-ocor.de-campo2 + it-doc-fisc.vl-ipi-it
       tt-tab-ocor.de-campo3 = tt-tab-ocor.de-campo3 + (IF  doc-fiscal.tipo-nat = 3
                                                        AND AVAIL natur-oper 
                                                        AND natur-oper.usa-pick 
                                                        AND natur-oper.cd-trib-ipi = 2
                                                        THEN it-doc-fisc.vl-tot-item 
                                                        ELSE IF  doc-fiscal.tipo-nat <> 3
                                                                 THEN it-doc-fisc.vl-ipint-it
                                                                 ELSE 0)
       tt-tab-ocor.de-campo4 = tt-tab-ocor.de-campo4 + (IF  doc-fiscal.tipo-nat = 3
                                                        AND AVAIL natur-oper 
                                                        AND natur-oper.usa-pick 
                                                        AND natur-oper.cd-trib-ipi = 3
                                                        THEN it-doc-fisc.vl-tot-item 
                                                        ELSE IF  doc-fiscal.tipo-nat <> 3
                                                                 THEN it-doc-fisc.vl-ipiou-it
                                                                 ELSE 0).            

/* Fim da include */
